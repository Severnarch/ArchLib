plugins {
    id 'java'
    id 'maven-publish'
}

group = 'io.github.severnarch'
version = '1.1.0'
String mcVersion = '1.20.1'
String fullVersion = "MC"+mcVersion+"-"+version

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    implementation 'org.jetbrains:annotations:24.0.0'
    compileOnly "org.spigotmc:spigot-api:"+mcVersion+"-R0.1-SNAPSHOT"
}

def targetJavaVersion = 17
java {
    toolchain {
        implementation = JvmImplementation.J9
        languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }

    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
}

publishing {
    publications {
        maven(MavenPublication) {
		    from components.java
        }
    }
}

jar {
    archiveFileName.set "ArchLib-"+fullVersion+".jar"
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: fullVersion]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
